IF EXISTS(SELECT NAME FROM SYSOBJECTS WHERE NAME= 'SPK_RPT_GLOSAREC' AND TYPE='P')
BEGIN
   DROP PROCEDURE SPK_RPT_GLOSAREC
END
GO
-- ------------------------------------------------------------------------------------
CREATE PROCEDURE DBO.SPK_RPT_GLOSAREC
@TIPO     INT,
@RPDX     VARCHAR(20),
@FECHAINI DATETIME,
@FECHAFIN DATETIME
WITH ENCRYPTION
AS
DECLARE @IDCONTRATO    VARCHAR(20) 
DECLARE @TIPORESTA     VARCHAR(1)
DECLARE @TOTALFACTURA  DECIMAL(14,2)
DECLARE @TOTALSERVICIO DECIMAL(14,2)
BEGIN   
   IF @TIPO = 1   -- SIN ENVIAR A AUDITORIA
   BEGIN
      INSERT INTO RPDX(CNS, IDTERCERO, VALOR1, VALOR2, VALOR3, VALOR4 )
      SELECT @RPDX, IDTERCERO, SUM(VR_TOTAL), SUM(VLRGLOSA), 0 , 0
      FROM   VWK_GLOSAS  
      WHERE  FECHA_AUD IS NULL 
      GROUP  BY IDTERCERO   
   END
   IF @TIPO = 2   -- RECIBIDAS EN AUDITORIA ENTRE FECHAS
   BEGIN
      INSERT INTO RPDX(CNS, IDTERCERO, VALOR1, VALOR2, VALOR3, VALOR4 )
      SELECT @RPDX, IDTERCERO, SUM(VR_TOTAL), SUM(VLRGLOSA), SUM(VLRACEPTADO), SUM(VLRRECUPERAR)
      FROM   VWK_GLOSAS  
      WHERE  FECHA_AUD BETWEEN @FECHAINI AND @FECHAFIN
      GROUP  BY IDTERCERO
   END
   IF @TIPO = 3   -- RESPONDIDAS EN AUDITORIA NO DEVUELTAS A CARTERA POR FECHA DE RESPUESTA
   BEGIN
      INSERT INTO RPDX(CNS, IDTERCERO, VALOR1, VALOR2, VALOR3, VALOR4 )
      SELECT @RPDX, IDTERCERO, SUM(VR_TOTAL), SUM(VLRGLOSA), SUM(VLRACEPTADO), SUM(VLRRECUPERAR)
      FROM   VWK_GLOSAS  
      WHERE  FECHA_AUD IS NOT NULL
      AND    FECHA_CAR IS NULL
      AND    FECHARESP BETWEEN @FECHAINI AND @FECHAFIN
      AND    CERRADA   = 1
      GROUP  BY IDTERCERO
   END
   IF @TIPO = 4   -- RESPONDIDAS EN AUDITORIA NO DEVUELTAS A CARTERA SIN IMPORTAR
                  -- LA FECHA DE RESPUESTA
   BEGIN
      INSERT INTO RPDX(CNS, IDTERCERO, VALOR1, VALOR2, VALOR3, VALOR4 )
      SELECT @RPDX, IDTERCERO, SUM(VR_TOTAL), SUM(VLRGLOSA), SUM(VLRACEPTADO), SUM(VLRRECUPERAR)
      FROM   VWK_GLOSAS  
      WHERE  FECHA_AUD IS NOT NULL
      AND    FECHA_CAR IS NULL
      AND    CERRADA   = 1
      GROUP  BY IDTERCERO
   END
   IF @TIPO = 5   -- RESPONDIDAS EN AUDITORIA DEVUELTAS Y SIN DEVOLVER A CARTERA POR FECHA 
   BEGIN
      INSERT INTO RPDX(CNS, IDTERCERO, VALOR1, VALOR2, VALOR3, VALOR4 )
      SELECT @RPDX, IDTERCERO, SUM(VR_TOTAL), SUM(VLRGLOSA), SUM(VLRACEPTADO), SUM(VLRRECUPERAR)
      FROM   VWK_GLOSAS  
      WHERE  FECHA_AUD IS NOT NULL
      AND    FECHARESP BETWEEN @FECHAINI AND @FECHAFIN
      AND    CERRADA   = 1
      GROUP  BY IDTERCERO
   END
   IF @TIPO = 6   -- RESPONDIDAS EN AUDITORIA DEVUELTAS A CARTERA POR FECHA DE DEVOLUCION
   BEGIN
      INSERT INTO RPDX(CNS, IDTERCERO, VALOR1, VALOR2, VALOR3, VALOR4 )
      SELECT @RPDX, IDTERCERO, SUM(VR_TOTAL), SUM(VLRGLOSA), SUM(VLRACEPTADO), SUM(VLRRECUPERAR)
      FROM   VWK_GLOSAS  
      WHERE  FECHA_AUD IS NOT NULL
      AND    FECHA_CAR IS NOT NULL
      AND    FECHA_CAR BETWEEN @FECHAINI AND @FECHAFIN
      GROUP  BY IDTERCERO
   END
END
GO
-- --------------------------------------------------------------------------------
IF EXISTS(SELECT NAME FROM SYSOBJECTS WHERE NAME= 'SPK_RESPUESTAGLOSA' AND TYPE='P')
BEGIN
   DROP PROCEDURE SPK_RESPUESTAGLOSA 
END
GO
-- ---------------------------------------------------------------------------------
CREATE PROCEDURE DBO.SPK_RESPUESTAGLOSA 
@CNSGLO           VARCHAR(20),
@MODO             SMALLINT,
@SEDE             VARCHAR(5),
@COMPANIA         VARCHAR(2),
@USUARIO          VARCHAR(12),
@SYS_COMPUTERNAME VARCHAR(254)
AS
DECLARE @NVOCONSEC      VARCHAR(20)
DECLARE @IDTERCERO      VARCHAR(20)
DECLARE @N_FACTURA      VARCHAR(16)
DECLARE @VLRACEPTADO    DECIMAL(14,2)
DECLARE @VLRRECUPERAR   DECIMAL(14,2)
DECLARE @CNSCXC         VARCHAR(20)
DECLARE @ITEM           INT  
DECLARE @TIPORESPUESTA  VARCHAR(1)    
DECLARE @CNSGLO_O       VARCHAR(20)
DECLARE @CNSRPDX        VARCHAR(20)
DECLARE @PROCEDENCIA    VARCHAR(20)
DECLARE @GN             SMALLINT 
DECLARE @NROCOMPROBANTE VARCHAR(20)
BEGIN
   UPDATE FGLOD SET VLRACEPTADO = 0 
   WHERE  VLRACEPTADO IS NULL
   AND    CNSGLO = @CNSGLO
   
   SELECT @TIPORESPUESTA = TIPORESPUESTA, @PROCEDENCIA = PROCEDENCIA,
          @GN = GENERANOTA, @NROCOMPROBANTE = NROCOMPROBANTE, @CNSGLO_O=COALESCE(CNSGLO_O,'') 
   FROM   FGLO 
   WHERE  CNSGLO = @CNSGLO
   
   IF @MODO = 1
   BEGIN
      UPDATE FGLOD SET VLRACEPTADO = VLRGLOSA, VLRRECUPERAR = 0 
      WHERE CNSGLO = @CNSGLO
      AND VLRGLOSA <> (VLRACEPTADO + VLRRECUPERAR)
   END
   IF @MODO = 2 
   BEGIN
      UPDATE FGLOD SET VLRRECUPERAR = VLRGLOSA, VLRACEPTADO = 0 
      WHERE CNSGLO = @CNSGLO
      AND VLRGLOSA <> (VLRACEPTADO + VLRRECUPERAR)
   END
   
   --ACTUALIZA VALORES EN GLOSA DESDE DETALLES
   IF @TIPORESPUESTA = 'I'
   BEGIN
      PRINT 'POR ITEM'
      SELECT @VLRACEPTADO = SUM(COALESCE(VLRACEPTADO,0)), @VLRRECUPERAR = SUM(COALESCE(VLRRECUPERAR,0))
      FROM   FGLOD
      WHERE  CNSGLO = @CNSGLO
      
      UPDATE FGLO SET VLRACEPTADO = @VLRACEPTADO, VLRRECUPERAR = @VLRRECUPERAR
      WHERE  CNSGLO = @CNSGLO
   END
   ELSE
   BEGIN
      PRINT 'TOTAL'
      SELECT @VLRACEPTADO = VLRACEPTADO, @VLRRECUPERAR = VLRRECUPERAR
      FROM   FGLO
      WHERE  CNSGLO = @CNSGLO
   END
   -- CREA NOTA CREDITO SI EL VALOR ACEPTADO ES > 0
   PRINT 'AQUI'   
   PRINT 'VLR ACEPTADO  = ' + STR(@VLRACEPTADO)
   PRINT 'VLR RECUPERAR = ' + STR(@VLRRECUPERAR)
   PRINT 'FIN'
   
   UPDATE FGLO SET ABONADO = @VLRACEPTADO, SALDO = @VLRRECUPERAR
   WHERE  CNSGLO = @CNSGLO
   
   IF (@PROCEDENCIA = 'Recaudo') OR (@PROCEDENCIA = 'Auditoria' AND @GN = 1) 
   BEGIN
      IF @VLRACEPTADO > 0    
      BEGIN
         --SE GENERA CONSECUTIVO DE NOTA
         EXEC SPK_GENCONSECUTIVO @COMPANIA,@SEDE,'@FNOTC', @NVOCONSEC OUTPUT  
         SELECT @NVOCONSEC = @SEDE + REPLACE(SPACE(8 - LEN(@NVOCONSEC))+LTRIM(RTRIM(@NVOCONSEC)),SPACE(1),0)      
         --SE INSERTAN LOS DATOS DE LA NOTA DESDE LA GLOSA
         INSERT INTO FNOT(CNSFNOT, CLASE, N_FACTURA, IDTERCERO, F_NOTA, VR_TOTAL, OBSERVACION, 
                          MARCA, MARCACONT, CONTABILIZADA, NROCOMPROBANTE, IMPRESO, COMPANIA,
                          USUARIO, CNSCXC, CERRADA, ESTADO, USUARIOANU, USUARIOSOL, FECHAANU,
                          RAZONANU, PROCEDENCIA, CNSGLO, APLICADAA )  
         SELECT @NVOCONSEC, 'C', N_FACTURA, IDTERCERO, GETDATE(), VLRACEPTADO, OBSERVACION,
                0, 0, 0, NULL, 0, @COMPANIA, @USUARIO, CNSCXC, 0, 'O', NULL, NULL, NULL, NULL,
                'AUDITORIA', CNSGLO, 'C'
         FROM   FGLO
         WHERE  CNSGLO = @CNSGLO
         -- SE CREA TABLA TEMPORAL PARA PODER MANEJAR EL ITEM DE MANERA IDENTITY
         CREATE TABLE #FNOTD(CNSFNOT VARCHAR(20), CLASE VARCHAR(1), ITEM SMALLINT IDENTITY, 
                       TIPO VARCHAR(1), IDSERVICIO VARCHAR(20), DESCRIPCION VARCHAR(512), 
                       CANTIDAD INT, VR_UNITARIO FLOAT , VR_TOTAL FLOAT, COMPANIA VARCHAR(2), 
                       USUARIO VARCHAR(12), OBSERVACION VARCHAR(2048))
         -- SE CREAN LOS DETALLES DE LA NOTA CREDITO DESDE LOS DETALLE DE LA GLOSA
         IF @TIPORESPUESTA = 'I'
         BEGIN
            INSERT INTO #FNOTD(CNSFNOT, CLASE, TIPO, IDSERVICIO, DESCRIPCION, CANTIDAD,
                               VR_UNITARIO, VR_TOTAL, COMPANIA, USUARIO, OBSERVACION)
            SELECT @NVOCONSEC, 'C', TIPO, IDSERVICIO, DESCRIPCION, 1, VLRACEPTADO,
                   VLRACEPTADO, @COMPANIA, @USUARIO, NULL
            FROM   FGLOD   
            WHERE  CNSGLO = @CNSGLO  
            AND    VLRACEPTADO > 0
         END
         ELSE                   
         BEGIN
            INSERT INTO #FNOTD(CNSFNOT, CLASE, TIPO, IDSERVICIO, DESCRIPCION, CANTIDAD,
                               VR_UNITARIO, VR_TOTAL, COMPANIA, USUARIO, OBSERVACION)
            SELECT @NVOCONSEC, 'C', 'C', FGLO.IDCONCEPTO, CPNT.DESCRIPCION, 1, FGLO.VLRACEPTADO,
                   FGLO.VLRACEPTADO, @COMPANIA, @USUARIO, FGLO.OBSERVACIONRTA
            FROM   FGLO, CPNT   
            WHERE  FGLO.CNSGLO = @CNSGLO  
            AND    FGLO.IDCONCEPTO = CPNT.CODIGO
            AND    FGLO.VLRACEPTADO > 0
         END
         -- SE INSERTAN LOS DATOS A DETALLES DE NOTA DESDE LA TABLA TEMPORAL
         INSERT INTO FNOTD(CNSFNOT, CLASE, ITEM, TIPO, IDSERVICIO, DESCRIPCION, CANTIDAD,
                           VR_UNITARIO, VR_TOTAL, COMPANIA, USUARIO, OBSERVACION)
         SELECT CNSFNOT, CLASE, ITEM, TIPO, IDSERVICIO, DESCRIPCION, CANTIDAD,
                VR_UNITARIO, VR_TOTAL, COMPANIA, USUARIO, OBSERVACION
         FROM #FNOTD 
         -- SE BORRA TABLA TEMPORAL
         
         DROP TABLE #FNOTD
         UPDATE FGLO SET CNSFNOT = @NVOCONSEC
         WHERE  CNSGLO = @CNSGLO
              
      END    
   
   END
   
   -- SE GENERA SALDO DE LO QUE SE VA A RECUPERAR
   IF @VLRRECUPERAR > 0 
   BEGIN
      -- SE COLOCA COMO SALDO DE LA GLOSA EL VALOR A RECUPERAR
      
      SELECT @CNSCXC = CNSCXC, @N_FACTURA = N_FACTURA 
      FROM   FGLO
      WHERE  CNSGLO = @CNSGLO
      
      SELECT @ITEM  = MAX(ITEM) 
      FROM   FCXCDV 
      WHERE  CNSCXC    = @CNSCXC
      AND    N_FACTURA = @N_FACTURA
            
      INSERT INTO FCXCDV(CNSCXC, N_FACTURA, CNSGLO, TIPO, SALDONETO, FECHA, 
                         F_RECIBIDO, F_VENCE, SALDOINICIAL,MARCAPAGO) 
      SELECT @CNSCXC, @N_FACTURA, @CNSGLO, 
             CASE @CNSGLO_O WHEN '' THEN 'G' ELSE 'R' END, @VLRRECUPERAR, GETDATE(), 
             NULL, NULL, @VLRRECUPERAR, 0
   END
   -- SE CIERRA LA GLOSA Y SUS ITEMES
   UPDATE FGLOD SET CERRADA = 1 WHERE CNSGLO = @CNSGLO
   UPDATE FGLO SET CERRADA = 1, FECHARESP = GETDATE()  WHERE CNSGLO = @CNSGLO
   -- SE LLAMA AL SPK QUE CIERRA LA NOTA, LA APLICA Y RELIQUIDA CXC
   -- (INCLUYENDO GLOSAS POR QUE LA ENCUENTRA ESTA CERRADA)
   DECLARE @SALDOPF DECIMAL(14,2)
   SELECT @SALDOPF = SALDO FROM FGLO
   WHERE  CNSGLO = @CNSGLO
   PRINT 'SALDO DE GLOSA ANTES DE NOTAS ='+ STR(@SALDOPF)
   SELECT @CNSRPDX = 'INTERNO'
   EXEC SPK_NOTASCXC @NVOCONSEC, 'C', @CNSRPDX, @SEDE, @COMPANIA, @USUARIO, @SYS_COMPUTERNAME
   IF (SELECT CERRADA FROM FGLO WHERE CNSGLO = @CNSGLO)=1
   BEGIN
      PRINT 'Contabilización Automática.'
      -- EXEC SPK_CONTAB_FGLO @CNSGLO, @USUARIO, @SYS_COMPUTERNAME, @COMPANIA, @SEDE, @NROCOMPROBANTE
   END
END
GO 
-- ----------------------------------------------
IF EXISTS (SELECT name FROM sysobjects 
   WHERE NAME = 'SPK_ENTGLOSAS_RESP ' AND type = 'P')
   DROP PROCEDURE SPK_ENTGLOSAS_RESP 
GO
-- ----------------------------------------------
CREATE PROCEDURE DBO.SPK_ENTGLOSAS_RESP
@CNSENTREGA VARCHAR(20),
@CNSGLOI    VARCHAR(20)
AS
DECLARE @PROCESO VARCHAR(20)
BEGIN
   SELECT @PROCESO = PROCESO FROM ENT WHERE CNSENTREGA = @CNSENTREGA
   INSERT INTO ENTD ( CNSENTREGA, NODOCUMENTO, PROCESO, ESTADO, MARCA )
   SELECT @CNSENTREGA, FGLOID.CNSGLO, @PROCESO, 0, 0
   FROM   FGLOID INNER JOIN FGLO ON FGLOID.CNSGLO = FGLO.CNSGLO
   WHERE  FGLOID.CNSGLOI = @CNSGLOI
   AND    FGLO.ASIGDEV = 0 
   AND    FGLO.CERRADA = 1
   AND    FGLO.IDDEP   = DBO.FNK_VALORVARIABLE('IDFDEPAUDITORIA')
END
GO

