IF EXISTS(SELECT NAME FROM SYS.OBJECTS WHERE NAME = 'SPK_SENDSMS' AND TYPE='P')
	DROP PROCEDURE SPK_SENDSMS

GO
CREATE PROCEDURE DBO.SPK_SENDSMS
(
	@NUMERO VARCHAR(13),
	@MENSAJE VARCHAR(8000),
	@METHOD VARCHAR(4)='GET' 
)
WITH ENCRYPTION
AS
DECLARE @sUrl VARCHAR(3096) 
DECLARE @obj INT
DECLARE @valorDeRegreso INT
DECLARE @response VARCHAR(8000)
DECLARE @src VARCHAR(255)
DECLARE @desc VARCHAR(255)
BEGIN
	
	IF DBO.FNK_VALORVARIABLE('SMS_HABILITADO')<>'SI' RETURN
	
	IF (SELECT COUNT(*) FROM TGEN WHERE TABLA='General' AND CAMPO='EXCLUYE_CELULAR' AND CODIGO=@NUMERO)>0 RETURN

	DECLARE @SIGLAS VARCHAR(20)=''
	SELECT @SIGLAS=@SIGLAS+LEFT(WORD,1) 
	FROM DBO.FNK_EXPLODE((SELECT RAZONSOCIAL FROM TER WHERE IDTERCERO=DBO.FNK_VALORVARIABLE('IDTERCEROINSTALADO')),' ')
	WHERE LEN(REPLACE(WORD,'.',''))>3

	IF LEN(@SIGLAS)=1 SELECT @SIGLAS=LEFT(RAZONSOCIAL,2) FROM TER WHERE IDTERCERO=DBO.FNK_VALORVARIABLE('IDTERCEROINSTALADO')

	SELECT @MENSAJE='('+@SIGLAS+') '+@MENSAJE

	--PRINT @MENSAJE

	SELECT @sUrl = OBSERVACION FROM USVGS WHERE IDVARIABLE='URL_API_SMS'
	IF ISNULL(@sUrl,'')=''
    BEGIN
       RAISERROR('VARIABLE DE SISTEMA (URL_API_SMS) NO CONFIGURADA',16,1) 
       RETURN
    END
		
	EXEC sys.sp_OACreate 'MSXML2.ServerXMLHttp', @obj OUT

	IF @METHOD='GET'
	BEGIN
		SET @sUrl = @sUrl + @NUMERO + '/' + @MENSAJE
		EXEC sys.sp_OAMethod @obj, 'Open', NULL, 'GET', @sUrl, false
		EXEC sys.sp_OAMethod @obj, 'send'
		EXEC sys.sp_OAGetProperty @obj, 'responseText', @response OUT
	END
	ELSE
	BEGIN
		SET @sUrl = @sUrl + @NUMERO
		DECLARE @Body AS VARCHAR(8000) ='{"msj":"' + @MENSAJE + '"}' 

		EXEC sys.sp_OAMethod @obj, 'Open', NULL, 'POST', @sUrl, false
		EXEC sys.sp_OAMethod @obj, 'setRequestHeader', null, 'Content-Type', 'application/json'
		EXEC SYS.sp_OAMethod @obj, 'send', null, @Body
		EXEC sys.sp_OAMethod @obj, 'responseText', @response OUTPUT
	END
	EXEC sys.sp_OADestroy @obj
		--SELECT @response [response]
END